!   doclets.ecs

    script Doclets

!    debug compile

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Doclet query system

    use graphics
    use mqtt

    window Window
    window MDWindow
    window TopicsWindow
    layout Layout
    layout MainPanel
    layout Panel1
    layout Panel2
    layout MDMainPanel
    layout ButtonPanel
    label Label
    label TopicsLabel
    multiline QueryInput
    multiline MDPanel
    pushbutton ChooseTopicsButton
    pushbutton SendQueryButton
    pushbutton ExitButton
    pushbutton MDCloseButton
    pushbutton OKButton
    checkbox TopicCheckbox
    checkbox LLMCheckbox
    listbox ResultsList

    topic MyTopic
    topic ServerTopic
    variable UUID
    variable Topics
    variable Query
    variable Message
    variable State
    variable Action
    variable MessageText
    variable ConfigFileName
    variable Text
    variable Items
    variable M
    variable N
    variable P
    dictionary Config
    dictionary Payload
    dictionary ReceivedMessage
    list TopicsAvailable
    list DocletList

!    debug step

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Set up the UI

    create MainPanel type QVBoxLayout

    create Layout type QHBoxLayout
    add Layout to MainPanel
    create Label text `Topics:` width 100
    add Label to Layout
    create TopicsLabel size 100
    add stretch TopicsLabel to Layout
    create ChooseTopicsButton text `Choose`
    on click ChooseTopicsButton go to ChooseTopics
    disable ChooseTopicsButton
    add ChooseTopicsButton to Layout

    create Layout type QHBoxLayout
    add Layout to MainPanel
    create Label text `Query:` width 100
    add Label to Layout
    create QueryInput cols 80 rows 5
    add QueryInput to Layout
    create ButtonPanel type QVBoxLayout
    add ButtonPanel to Layout
    create LLMCheckbox text `Use LLM`
    add LLMCheckbox to ButtonPanel
    create SendQueryButton text `Send`
    disable SendQueryButton
    on click SendQueryButton go to SendQueryClick
    add SendQueryButton to ButtonPanel

    create Layout type QHBoxLayout
    add Layout to MainPanel
    create Label text `Results:` width 100
    add Label to Layout
    create ResultsList
    add ResultsList to Layout
    on select ResultsList go to ResultsListClick

    create Layout type QHBoxLayout
    add Layout to MainPanel
!    create Label text `` width 100
!    add Label to Layout
    create ExitButton text `Exit`
    add ExitButton to Layout
    on select ExitButton go to ExitButtonClick

    create Window title `Doclets`
    set the layout of Window to MainPanel
    show Window
    adjust Window

    put `~/.doclets_config.json` into ConfigFileName
    if file ConfigFileName exists load Config from ConfigFileName
    else
    begin
        reset Config
        set entry `topics` of Config to empty
    end
    if entry `topics` of Config is empty set the text of TopicsLabel to `(none chosen)`
    else set the text of TopicsLabel to entry `topics` of Config

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Set up MQTT

    put uuid into UUID

    init MyTopic
        name `Doclets-` cat UUID
        qos 1
    
    init ServerTopic
        name `a8:41:f4:d3:19:dd/request`
        qos 1

    mqtt
        id UUID
        broker `test.mosquitto.org`
        port 1883
        subscribe MyTopic
        action `query` requires `topics` and `message`
        action `view` requires `message`
    
    on mqtt message
    begin
        put the mqtt message into ReceivedMessage
!        log `Received ` cat ReceivedMessage
        gosub to ProcessMessage
    end
    
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Initialise things
    put `idle` into State

    wait 3
    
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Request the list of topics from the hub
    put `topics` into State
    send to ServerTopic
        sender MyTopic
        action `topics`
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Here when the user clicks the Choose button
ChooseTopics:
    create TopicsWindow title `Topics`
    create Panel1 type QVBoxLayout
    set the layout of TopicsWindow to Panel1
    create Panel2 type QVBoxLayout
    add Panel2 to Panel1
    set the elements of TopicCheckbox to the count of TopicsAvailable
    put TopicsLabel into Items
    split Items on `,`
    put 0 into N
    while N is less than the count of TopicsAvailable
    begin
        put item N of TopicsAvailable into Text
        index TopicCheckbox to N
        create TopicCheckbox text Text
        put 0 into M
        while M is less than the elements of Items
        begin
            index Items to M
            if Text is Items
            begin
                set the state of TopicCheckbox to true
                go to CT2
            end
            increment M
        end
    CT2:
        add TopicCheckbox to Panel2
        increment N
    end
    create OKButton text `OK`
    add OKButton to Panel1
    on click OKButton
    begin
        put empty into Topics
        put 0 into N
        while N is less than the count of TopicsAvailable
        begin
            index TopicCheckbox to N
            if TopicCheckbox
            begin
                if Topics is not empty put Topics cat `,` into Topics
                put Topics cat item N of TopicsAvailable into Topics
            end
            increment N
        end
        log Topics
        set the text of TopicsLabel to Topics
        close TopicsWindow
    end
    show TopicsWindow
    adjust TopicsWindow
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Here when the user clicks the Send button
SendQueryClick:
    put TopicsLabel into Topics
    put QueryInput into Query
    if LLMCheckbox put `LLM:` cat Query into Query
    put `query` into State
!    send mqtt `path:` cat Topics to ServerTopic
    clear ResultsList
    send to ServerTopic
        sender MyTopic
        action `query`
        topics Topics
        message Query
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Here when the user clicks an item in the results list
ResultsListClick:
    log `Select ` cat ResultsList
    put ResultsList into Query
    put the position of `:` in Query into P
    put left P of Query into Query
    put `content` into State
    send to ServerTopic
        sender MyTopic
        action `view`
        message Query
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Here when the user clicks the exit button
ExitButtonClick:
    set entry `topics` of Config to the text of TopicsLabel
    log Config
    save Config to ConfigFileName
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Process a received message
ProcessMessage:
    log `Message received re ` cat State
    put the mqtt message into ReceivedMessage
!    log ReceivedMessage
    put entry `message` of ReceivedMessage into MessageText
    put entry `action` of ReceivedMessage into Action
    if State is `topics`
    begin
        if Action is `confirm` gosub to SendConfirmation
        put MessageText into TopicsAvailable
        log `Topics available: ` cat TopicsAvailable
        enable ChooseTopicsButton
        enable SendQueryButton
    end
    else if State is `query`
    begin
        log MessageText
        if MessageText is `[]` set DocletList to `["No results"]`
        set ResultsList to json MessageText
        log ResultsList
        set DocletList to ResultsList
    end
    else if State is `content`
    begin
        create MDMainPanel type QVBoxLayout

        create MDPanel cols 80 rows 25
        set the text of MDPanel to Message
        add stretch MDPanel to MDMainPanel
        create MDCloseButton text `Close`
        add MDCloseButton to MDMainPanel
        on click MDCloseButton close MDWindow

        create MDWindow title ResultsList !size 700 500
        set the layout of MDWindow to MDMainPanel
        show MDWindow
        adjust MDWindow
    end
    return

! Send a confirmtion message
SendConfirmation:
    send to ServerTopic
        sender MyTopic
        action `confirm`
    return