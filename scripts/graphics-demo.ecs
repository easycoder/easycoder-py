!   Graphics demo

    script Graphics-Demo

    rectangle BlueRect
    ellipse RedEllipse
    ellipse GreenCircle
    file File
    variable Spec
    variable UpDown
    variable Left
    variable Top
    variable Steps
    variable Step
    variable DX
    variable DY
    variable N
    variable X
    variable Y
    variable L
    variable T

!    debug step

    create screen at 300 300 size 640 480 fill color yellow

    open File `json/graphics-demo.json` for reading
    read Spec from File
    close File

    render Spec

    attach BlueRect to `bluerect`
    on click BlueRect print `Click Blue`
    attach RedEllipse to `redellipse`
    on click RedEllipse print `Click Red`
    attach GreenCircle to `greencircle`
    on click GreenCircle print `Click Green`
    
    gosub to InitEllipse

    clear UpDown
    put 0 into N
    on tick
    begin
        if UpDown
        begin
            if N is less than 10
            begin
                move BlueRect by -10 -10
            end
            else
            begin
                put 0 into N
                clear UpDown
            end
        end
        else
        begin
            if N is less than 10
            begin
                move BlueRect by 10 10
            end
            else
            begin
                put 0 into N
                set UpDown
            end
        end
        increment N
        gosub to MoveEllipse
    end
    show screen
    stop

InitEllipse:
    put attribute `left` of RedEllipse into Left
    put attribute `top` of RedEllipse into Top
    put random 340 into X
    put random 280 into Y
    add 50 to X
    add 50 to Y
    take Left from X giving DX
    take Top from Y giving DY
    put 20 into Steps
    put 0 into Step
    return

MoveEllipse:
    multiply DX by Step giving X
    divide X by Steps
    add X to Left giving L
    multiply DY by Step giving Y
    divide Y by Steps
    add Y to Top giving T
    move RedEllipse to L T
    increment Step
    if Step is Steps gosub to InitEllipse
    return
